apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

group = 'br.com.eits.syncer'
version = '0.0.6-SNAPSHOT'
project.archivesBaseName = 'eits-syncer-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 21
        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
    }

    lintOptions {
        abortOnError false
    }
}

def feignVersion = '9.3.1'

dependencies {
    compile 'io.requery:sqlite-android:3.15.1'
    compile ( 'br.com.eits.syncer:eits-syncer-java:'+version ) {
        exclude module: 'hibernate-envers'
    }
    compile 'io.github.openfeign:feign-core:'+feignVersion
    compile 'io.github.openfeign:feign-jackson:'+feignVersion
    compile ('io.github.openfeign:feign-jaxrs:'+feignVersion) {
        exclude module: 'jsr311-api'
    }


}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}